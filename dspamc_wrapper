#!/usr/bin/perl

# Copyright (c) 2012,2015 Todd T. Fries <todd@fries.net>
#
# Permission to use, copy, modify, and distribute this software for any
# purpose with or without fee is hereby granted, provided that the above
# copyright notice and this permission notice appear in all copies.
#
# THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
# WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
# ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
# WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
# ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
# OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.

use strict;
use warnings;

use Sys::Syslog qw(:standard :macros);

openlog("dspamc_wrapper", "ndelay,pid", "mail");
our $starttime = time();

# Is there a better way to insert a header than to futz in this way?

our $verbose = 0;
if (defined($ARGV[0]) && $ARGV[0] eq "-v") {
	$verbose++;
	shift @ARGV;
}

my $rcpt = $ARGV[0];
my $envfrom = $ARGV[1];

my $config_file = "/etc/dspamwrap.conf";

my $skiptore;
my $scan = 1;
my $maxmessagesize = 1048576;

if (-f $config_file) {
	eval {
		eval `cat $config_file`;
	}
}
if ($verbose>0) {
	syslog(LOG_INFO, "past conf file\n");
}

if (!defined($rcpt) || !defined($envfrom)) {
	print STDERR "Usage: dspamc_wrapper <rcpt> <from>\n";
	exit(1);
}

syslog(LOG_INFO, "%s -> %s", $envfrom, $rcpt);

if ($scan == 1 && defined($skiptore)) {
	if ($rcpt =~ m/${skiptore}/i) {
		$scan = 0;
	}
}


our ($fhread, $fhwrite);
our $pid;
if ($verbose > 0) {
	syslog(LOG_INFO, "scan=%d", $scan);
}

if ($scan) {
	use IPC::Open2;
	use FileHandle;

	$SIG{'PIPE'} = 'IGNORE';

	my $cmd = sprintf "%s --client --stdout --deliver=innocent,spam --user %s",
	    "/usr/local/bin/dspamc",lc($rcpt);
	if ($verbose > 0) {
		syslog(LOG_INFO,sprintf("cmd=%s\n",$cmd));
	}

	$pid = open2($fhread,$fhwrite,${cmd});
	if (!defined($pid)) {
		my $msg=$!;
		syslog(LOG_INFO,"Can't fork: %s",$msg);
		exit(75); # tempfail
	}
	if ($verbose > 0) {
		syslog(LOG_INFO,sprintf("pid=%d\n",$pid));
	}
	$SIG{'TERM'} = sub {
		my $signame = shift;
		syslog(LOG_INFO,"Got %s, killing child pid %d", $signame, $pid);
		closefhwrite();
		$fhread->close();
		kill('KILL', $pid);
		exit(75);
	};
}
my @headers;
my @body;
my $state = 0; # 0 = headers, 1 = body
my $linecount = 0;
my $msgsize = 0;
my $scansize = 0;
while(<STDIN>) {
	my $line = $_;
	$msgsize += length($line);
	if ($verbose > 0 && ($linecount++ % 1000) == 0) {
		syslog(LOG_INFO,sprintf("made it to line %d\n",$linecount));
	}
	# do not train based on X-(DSPAM|FDH)- headers, so show them below
	if ($state == 0) {
		if (/^$/) {
			$state++;
			if ($scan) {
				if ($verbose > 0) {
					syslog(LOG_INFO,"pre-fhwrite: %s",$_);
				}
				eval {
					$fhwrite->print($_);
				};
				if ($@) {
					syslog(LOG_INFO,sprintf("FHWRITE failed: %s\n",$@));
				}
				if ($verbose > 0) {
					syslog(LOG_INFO,sprintf("post-fhwrite: %s",$_));
				}

			}
			next;
		}
		if ($scan == 1 && (/^X-o?DSPAM-/ || /^X-o?FDH-/)) {
			$line =~ s/-(DSPAM|FDH)-/-o${1}-/;
			push @headers,$line;
			next;
		}
		if ($verbose>1) {
			syslog(LOG_INFO,"+header: %s",$line);
		}
		push @headers,$line;
		if ($scan) {
			eval {
				$fhwrite->print($_);
			};
			if ($@) {
				syslog(LOG_INFO,"FHWRITE failed: %s",$@);
			}
		}
		next;
	}
	if ($verbose>1) {
		syslog(LOG_INFO,"+body: %s", $line);
	}
	push @body,$line;
	if ($scan && $msgsize < $maxmessagesize) {
		$scansize = $msgsize;
		eval {
			$fhwrite->print($_);
		};
		if ($@) {
			syslog(LOG_INFO,sprintf("FHWRITE failed: %s\n",$@));
		}
	}
}
if ($verbose > 0) {
	syslog(LOG_INFO,"STDIN reading done");
}
if ($scan) {
	closefhwrite();
	if ($verbose>0) {
		syslog(LOG_INFO,"closefhwrite()");
	}

	# states:
	#
	# 0 = starting
	# 1 = Seen first header
	
	
	$state = 0;
	eval {
	while(<$fhread>) {
		my $h = $_;
		if ($verbose>0) {
			syslog(LOG_INFO,"<\$fhread>");
		}
		if ($state == 0) {
			if ($h =~ /^X-DSPAM/) {
				push @headers,$h;
			}
			if ($h =~ /^$/) {
				$state++;
				last;
			}
		}
		next;
	}
	$fhread->close();
	};
	if ($@) {
		syslog(LOG_INFO,"FHREAD failed: %s",$@);
		exit(75);
	}
	if ($verbose>0) {
		syslog(LOG_INFO,"FHREAD succeeded");
	}
}
foreach my $h (@headers) {
	printf "%s", $h;
}
printf "X-FDH-EnvFrom: %s\n", $envfrom;
printf "X-FDH-EnvTo: %s\n\n", $rcpt;
foreach my $b (@body) {
	printf "%s", $b;
}

our $stoptime = time();
our $totaltime = $stoptime - $starttime;
if ($scan) {
	my $ret = waitpid($pid, 0);
	if ($verbose>0) {
		syslog(LOG_INFO,"vars: sansize=$scansize, msgsize=$msgsize, ret=$ret, totaltime=$totaltime");
	}
	my $bsent;
	if ($scansize < $msgsize) {
		$bsent = sprintf("%d/%d",$scansize,$msgsize);
	} else {
		$bsent = sprintf("%d",$scansize);
	}
	my $speed;
	if ($totaltime == 0) {
		$speed = "<inf>";
	} else {
		$speed = sprintf("%0.3f",$scansize/$totaltime);
	}
	syslog(LOG_INFO,"ret=%d: sent %sb in %ds, %sb/s", $ret, $bsent, $totaltime, $speed);

	if ($verbose > 0) {
		syslog(LOG_INFO,sprintf("waitpid returned %d: status=$? native=${^CHILD_ERROR_NATIVE}\n",$ret));
	}
	if ($ret == $pid) {
		exit(0);
	}
	if ($ret == -1) {
		exit(0);
	}
	exit(75); # tempfail
}
closelog();
exit(0);

sub closefhwrite {
	eval {
		$fhwrite->flush();
		$fhwrite->close();
	};
	if ($@) {
		syslog(LOG_INFO,sprintf("FHWRITE failed: %s\n",$@));
	}
}
